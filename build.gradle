plugins {
     id "com.teamscale" version "21.9.0"
    id "com.gorylenko.gradle-git-properties" version "2.2.2"
    id 'jacoco'
    id 'org.ajoberstar.grgit' version '2.3.0'
}


teamscale {

    server {
        url = 'http://localhost:8080/'
        userName = 'admin'
        userAccessToken = 'q9N2q4nbn5R16ppaokFLRGBrnNGCFVPm'
        project = 'tia-junit5'
    }

    report {
        testwiseCoverage {
            partition = 'Unit Tests'
        }
    }

}


//-javaagent:teamscale-jacoco-agent/lib/teamscale-jacoco-agent.jar
//-javaagent:teamscale-jacoco-agent/lib/teamscale-jacoco-agent.jar=config-file=javaagent.properties




group 'eu.cqse.tutorials'
version '1.0-SNAPSHOT'

// Please make use of this toolchain feature also in
// your Gradle projects to specify the Java version 
// to use. Setting paths to particular JDK installations
// has shown to be error prone and is not supported by
// some Gradle plugins well.
java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(11))
  }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.5.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
}

tasks.withType(Test) {
    useJUnitPlatform()
    testLogging {
        exceptionFormat "short"
        showCauses false
        events "failed", "passed"
    }
}

jacoco {
    toolVersion = "0.8.7"
    reportsDirectory = layout.buildDirectory.dir('customJacocoReportDir')
}

test {
    jacoco {
        destinationFile = layout.buildDirectory.file('jacoco/jacocoTest.exec').get().asFile
        classDumpDir = layout.buildDirectory.dir('jacoco/classpathdumps').get().asFile
    }
}
tasks.register('tiaTests', com.teamscale.TestImpacted) {
    useJUnitPlatform()
    jacoco {
        includes = ["tia.*"]
    }
}


